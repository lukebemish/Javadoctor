import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'net.minecraftforge.gradleutils' version '[2.0.13,)'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

changelog {
    fromTag '1.0'
}

allprojects {
    println("${it.name} version: ${version = gradleutils.tagOffsetVersion}")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'net.neoforged.javadoctor'
    java {
        withSourcesJar()
    }

    if (name.startsWith('injector-')) {
        apply plugin: 'java-library'
        apply plugin: 'com.github.johnrengelman.shadow'
        apply plugin: 'application'

        application {
            mainClass.set('net.neoforged.javadoctor.injector.Main')
        }

        configurations {
            dependency
            api.extendsFrom(dependency)
        }
        evaluationDependsOn(':injector')
        dependencies {
            dependency project(':injector')
        }

        tasks.named('shadowJar', ShadowJar) {
            // JOPT doesn't like being relocated
            //    enableRelocation = true
            //    relocationPrefix = 'net.neoforged.javadoctor.injector.relocate'
            configurations = [project.configurations.dependency]
        }
    }

    repositories {
        mavenCentral {
            metadataSources {
                mavenPom()
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven gradleutils.getPublishingForgeMaven()
        }
    }
}