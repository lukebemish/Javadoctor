plugins {
    id 'java-library'
}

archivesBaseName = 'collector'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

sourceSets {
    test
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spongepowered.org/repository/maven-public/'
        content {
            includeGroup('org.spongepowered')
        }
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.10.1'
    api project(':spec')
    api project(':gson-io')
    compileOnly 'org.jetbrains:annotations:24.0.1'

    testAnnotationProcessor sourceSets.main.output
    testAnnotationProcessor 'com.google.code.gson:gson:2.10.1'
    testAnnotationProcessor project(':spec')
    testAnnotationProcessor project(':gson-io')
    testCompileOnly 'org.spongepowered:mixin:0.8.5'
}

tasks.named('compileTestJava', JavaCompile).configure {
    options.compilerArgs.add('-AcollectionPackages=hello,stab')
}

tasks.register('testJar', Jar) {
    it.archiveFileName.set('test.jar')
    it.from(sourceSets.test.output)
}